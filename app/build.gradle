apply plugin: 'com.android.application'

android {
    lintOptions {
        disable 'MissingTranslation'
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    compileSdkVersion 27
    defaultConfig {
        applicationId "com.ugetdm.uget"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 58
        versionName '2.1.3a'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
            ndk {
                // Specifies the ABI configurations of your native
                // libraries Gradle should build and package with your APK.
                // armeabi-v7a, arm64-v8a, x86, x86_64, mips, mips64
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions 'tier'
    productFlavors {
        noads {
            dimension 'tier'
            applicationId = "com.ugetdm.uget.noads"
            resValue "string", "app_flavor_name", "No Ads version"
            buildConfigField "boolean", "HAVE_ADS", "false"
        }
        ads {
            dimension 'tier'
            applicationId = "com.ugetdm.uget"
            resValue "string", "app_flavor_name", "Ads version"
            buildConfigField "boolean", "HAVE_ADS", "true"
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.android.gms:play-services-ads:12.0.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(':aFileDialog')
}
